#!/bin/sh
# -*- tcl -*-
# The next line is executed by /bin/sh, but not tcl \
exec tclsh "$0" ${1+"$@"}

#    This software is Copyright by the Board of Trustees of Michigan
#    State University (c) Copyright 2014.
#
#    You may use this software under the terms of the GNU public license
#    (GPL).  The terms of this license are described at:
#
#     http://www.gnu.org/licenses/gpl.txt
#
#    Authors:
#             Ron Fox
#             Jeromy Tompkins 
#	     NSCL
#	     Michigan State University
#	     East Lansing, MI 48824-1321



##
# @file stripparam.test
# @brief tests for the StripParam snit::type
# @author Ron Fox <fox@nscl.msu.edu>
#

package require tcltest
set here [file dirname [info script]]
source [file join $here channel.tcl]
source [file join $here stripparam.tcl]

tcltest::test construct {Ensure that construction saves the channel} \
-setup {
    Channel achannel
} \
-cleanup {
    achannel destroy
    stripItem destroy
} \
-body {
    stripParam stripItem -channel achannel
    
    stripItem cget -channel
} -result achannel

tcltest::test rate {rate faithfully returns the rate value} \
-setup {
    Channel achannel
} \
-cleanup {
    achannel destroy
    stripItem destroy
} \
-body {
    stripParam stripItem -channel achannel
    achannel update 123 1
    stripItem  rate
} -result 123.0

tcltest::test time {time faithfully returns the elapsed time} \
-setup {
    Channel achannel
} \
-cleanup {
    achannel destroy
    stripItem destroy
} \
-body {
    stripParam stripItem -channel achannel
    achannel update 123 5
    achannel update 1   5
    
    stripItem time
} -result 10

tcltest::test changed-no {Ensure hasUpdated is false if there was no change since last read} \
-setup {
    Channel achannel
    stripParam stripItem -channel achannel
} \
-cleanup {
    achannel destroy
    stripItem destroy
} \
-body {
    achannel update 1 2
    stripItem time
    
    stripItem hasUpdated
} -result 0

tcltest::test changed-yes {Ensure hasUpdated is true if there as a change since last read} \
-setup {
    Channel achannel
    stripParam stripItem -channel achannel
} \
-cleanup {
    achannel destroy
    stripItem destroy
} \
-body {
    achannel update 1 2
    stripItem time
    achannel update 1 2
    
    stripItem hasUpdated
} -result 1

tcltest::test clear {clearing should make hasUpdated be true} \
-setup {
    Channel achannel
    stripParam stripItem -channel achannel
} \
-cleanup {
    achannel destroy
    stripItem destroy
} \
-body {
    achannel update 1 2
    stripItem time
    stripItem clear
    
    stripItem hasUpdated
} -result 1

tcltest::cleanupTests