#/*
# 		    GNU GENERAL PUBLIC LICENSE
# 		       Version 2, June 1991
# 
#  Copyright (C) 1989, 1991 Free Software Foundation, Inc.
#                        59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
#  Everyone is permitted to copy and distribute verbatim copies
#  of this license document, but changing it is not allowed.
# 
# 			    Preamble
# 
#   The licenses for most software are designed to take away your
# freedom to share and change it.  By contrast, the GNU General Public
# License is intended to guarantee your freedom to share and change free
# software--to make sure the software is free for all its users.  This
# General Public License applies to most of the Free Software
# Foundation's software and to any other program whose authors commit to
# using it.  (Some other Free Software Foundation software is covered by
# the GNU Library General Public License instead.)  You can apply it to
# your programs, too.
# 
#   When we speak of free software, we are referring to freedom, not
# price.  Our General Public Licenses are designed to make sure that you
# have the freedom to distribute copies of free software (and charge for
# this service if you wish), that you receive source code or can get it
# if you want it, that you can change the software or use pieces of it
# in new free programs; and that you know you can do these things.
# 
#   To protect your rights, we need to make restrictions that forbid
# anyone to deny you these rights or to ask you to surrender the rights.
# These restrictions translate to certain responsibilities for you if you
# distribute copies of the software, or if you modify it.
# 
#   For example, if you distribute copies of such a program, whether
# gratis or for a fee, you must give the recipients all the rights that
# you have.  You must make sure that they, too, receive or can get the
# source code.  And you must show them these terms so they know their
# rights.
# 
#   We protect your rights with two steps: (1) copyright the software, and
# (2) offer you this license which gives you legal permission to copy,
# distribute and/or modify the software.
# 
#   Also, for each author's protection and ours, we want to make certain
# that everyone understands that there is no warranty for this free
# software.  If the software is modified by someone else and passed on, we
# want its recipients to know that what they have is not the original, so
# that any problems introduced by others will not reflect on the original
# authors' reputations.
# 
#   Finally, any free program is threatened constantly by software
# patents.  We wish to avoid the danger that redistributors of a free
# program will individually obtain patent licenses, in effect making the
# program proprietary.  To prevent this, we have made it clear that any
# patent must be licensed for everyone's free use or not licensed at all.
# 
#   The precise terms and conditions for copying, distribution and
# modification follow.
# 
# 		    GNU GENERAL PUBLIC LICENSE
#    TERMS AND CONDITIONS FOR COPYING, DISTRIBUTION AND MODIFICATION
# 
#   0. This License applies to any program or other work which contains
# a notice placed by the copyright holder saying it may be distributed
# under the terms of this General Public License.  The "Program", below,
# refers to any such program or work, and a "work based on the Program"
# means either the Program or any derivative work under copyright law:
# that is to say, a work containing the Program or a portion of it,
# either verbatim or with modifications and/or translated into another
# language.  (Hereinafter, translation is included without limitation in
# the term "modification".)  Each licensee is addressed as "you".
# 
# Activities other than copying, distribution and modification are not
# covered by this License; they are outside its scope.  The act of
# running the Program is not restricted, and the output from the Program
# is covered only if its contents constitute a work based on the
# Program (independent of having been made by running the Program).
# Whether that is true depends on what the Program does.
# 
#   1. You may copy and distribute verbatim copies of the Program's
# source code as you receive it, in any medium, provided that you
# conspicuously and appropriately publish on each copy an appropriate
# copyright notice and disclaimer of warranty; keep intact all the
# notices that refer to this License and to the absence of any warranty;
# and give any other recipients of the Program a copy of this License
# along with the Program.
# 
# You may charge a fee for the physical act of transferring a copy, and
# you may at your option offer warranty protection in exchange for a fee.
# 
#   2. You may modify your copy or copies of the Program or any portion
# of it, thus forming a work based on the Program, and copy and
# distribute such modifications or work under the terms of Section 1
# above, provided that you also meet all of these conditions:
# 
#     a) You must cause the modified files to carry prominent notices
#     stating that you changed the files and the date of any change.
# 
#     b) You must cause any work that you distribute or publish, that in
#     whole or in part contains or is derived from the Program or any
#     part thereof, to be licensed as a whole at no charge to all third
#     parties under the terms of this License.
# 
#     c) If the modified program normally reads commands interactively
#     when run, you must cause it, when started running for such
#     interactive use in the most ordinary way, to print or display an
#     announcement including an appropriate copyright notice and a
#     notice that there is no warranty (or else, saying that you provide
#     a warranty) and that users may redistribute the program under
#     these conditions, and telling the user how to view a copy of this
#     License.  (Exception: if the Program itself is interactive but
#     does not normally print such an announcement, your work based on
#     the Program is not required to print an announcement.)
# 
# These requirements apply to the modified work as a whole.  If
# identifiable sections of that work are not derived from the Program,
# and can be reasonably considered independent and separate works in
# themselves, then this License, and its terms, do not apply to those
# sections when you distribute them as separate works.  But when you
# distribute the same sections as part of a whole which is a work based
# on the Program, the distribution of the whole must be on the terms of
# this License, whose permissions for other licensees extend to the
# entire whole, and thus to each and every part regardless of who wrote it.
# 
# Thus, it is not the intent of this section to claim rights or contest
# your rights to work written entirely by you; rather, the intent is to
# exercise the right to control the distribution of derivative or
# collective works based on the Program.
# 
# In addition, mere aggregation of another work not based on the Program
# with the Program (or with a work based on the Program) on a volume of
# a storage or distribution medium does not bring the other work under
# the scope of this License.
# 
#   3. You may copy and distribute the Program (or a work based on it,
# under Section 2) in object code or executable form under the terms of
# Sections 1 and 2 above provided that you also do one of the following:
# 
#     a) Accompany it with the complete corresponding machine-readable
#     source code, which must be distributed under the terms of Sections
#     1 and 2 above on a medium customarily used for software interchange; or,
# 
#     b) Accompany it with a written offer, valid for at least three
#     years, to give any third party, for a charge no more than your
#     cost of physically performing source distribution, a complete
#     machine-readable copy of the corresponding source code, to be
#     distributed under the terms of Sections 1 and 2 above on a medium
#     customarily used for software interchange; or,
# 
#     c) Accompany it with the information you received as to the offer
#     to distribute corresponding source code.  (This alternative is
#     allowed only for noncommercial distribution and only if you
#     received the program in object code or executable form with such
#     an offer, in accord with Subsection b above.)
# 
# The source code for a work means the preferred form of the work for
# making modifications to it.  For an executable work, complete source
# code means all the source code for all modules it contains, plus any
# associated interface definition files, plus the scripts used to
# control compilation and installation of the executable.  However, as a
# special exception, the source code distributed need not include
# anything that is normally distributed (in either source or binary
# form) with the major components (compiler, kernel, and so on) of the
# operating system on which the executable runs, unless that component
# itself accompanies the executable.
# 
# If distribution of executable or object code is made by offering
# access to copy from a designated place, then offering equivalent
# access to copy the source code from the same place counts as
# distribution of the source code, even though third parties are not
# compelled to copy the source along with the object code.
# 
#   4. You may not copy, modify, sublicense, or distribute the Program
# except as expressly provided under this License.  Any attempt
# otherwise to copy, modify, sublicense or distribute the Program is
# void, and will automatically terminate your rights under this License.
# However, parties who have received copies, or rights, from you under
# this License will not have their licenses terminated so long as such
# parties remain in full compliance.
# 
#   5. You are not required to accept this License, since you have not
# signed it.  However, nothing else grants you permission to modify or
# distribute the Program or its derivative works.  These actions are
# prohibited by law if you do not accept this License.  Therefore, by
# modifying or distributing the Program (or any work based on the
# Program), you indicate your acceptance of this License to do so, and
# all its terms and conditions for copying, distributing or modifying
# the Program or works based on it.
# 
#   6. Each time you redistribute the Program (or any work based on the
# Program), the recipient automatically receives a license from the
# original licensor to copy, distribute or modify the Program subject to
# these terms and conditions.  You may not impose any further
# restrictions on the recipients' exercise of the rights granted herein.
# You are not responsible for enforcing compliance by third parties to
# this License.
# 
#   7. If, as a consequence of a court judgment or allegation of patent
# infringement or for any other reason (not limited to patent issues),
# conditions are imposed on you (whether by court order, agreement or
# otherwise) that contradict the conditions of this License, they do not
# excuse you from the conditions of this License.  If you cannot
# distribute so as to satisfy simultaneously your obligations under this
# License and any other pertinent obligations, then as a consequence you
# may not distribute the Program at all.  For example, if a patent
# license would not permit royalty-free redistribution of the Program by
# all those who receive copies directly or indirectly through you, then
# the only way you could satisfy both it and this License would be to
# refrain entirely from distribution of the Program.
# 
# If any portion of this section is held invalid or unenforceable under
# any particular circumstance, the balance of the section is intended to
# apply and the section as a whole is intended to apply in other
# circumstances.
# 
# It is not the purpose of this section to induce you to infringe any
# patents or other property right claims or to contest validity of any
# such claims; this section has the sole purpose of protecting the
# integrity of the free software distribution system, which is
# implemented by public license practices.  Many people have made
# generous contributions to the wide range of software distributed
# through that system in reliance on consistent application of that
# system; it is up to the author/donor to decide if he or she is willing
# to distribute software through any other system and a licensee cannot
# impose that choice.
# 
# This section is intended to make thoroughly clear what is believed to
# be a consequence of the rest of this License.
# 
#   8. If the distribution and/or use of the Program is restricted in
# certain countries either by patents or by copyrighted interfaces, the
# original copyright holder who places the Program under this License
# may add an explicit geographical distribution limitation excluding
# those countries, so that distribution is permitted only in or among
# countries not thus excluded.  In such case, this License incorporates
# the limitation as if written in the body of this License.
# 
#   9. The Free Software Foundation may publish revised and/or new versions of the General Public License from time to time.  Such new versions will be similar in spirit to the present version, but may differ in detail to address new problems or concerns.
# 
# Each version is given a distinguishing version number.  If the Program
# specifies a version number of this License which applies to it and "any
# later version", you have the option of following the terms and conditions 
# either of that version or of any later version published by the Free Software 
# Foundation.  If the Program does not specify a version number of this License,
#  you may choose any version ever published by the Free Software Foundation.
# 
#   10. If you wish to incorporate parts of the Program into other free
# programs whose distribution conditions are different, write to the author to 
# ask for permission.  For software which is copyrighted by the Free Software 
# Foundation, write to the Free Software Foundation; we sometimes make 
# exceptions for this.  Our decision will be guided by the two goals of 
# preserving the free status of all derivatives of our free software and of 
# promoting the sharing and reuse of software generally.
# 
# 			    NO WARRANTY
# 
#   11. BECAUSE THE PROGRAM IS LICENSED FREE OF CHARGE, THERE IS NO WARRANTY FOR
# THE PROGRAM, TO THE EXTENT PERMITTED BY APPLICABLE LAW.  EXCEPT WHEN 
# OTHERWISE STATED IN WRITING THE COPYRIGHT HOLDERS AND/OR OTHER PARTIES PROVIDE 
# THE PROGRAM "AS IS" WITHOUT WARRANTY OF ANY KIND, EITHER EXPRESSED OR IMPLIED, 
# INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND 
# FITNESS FOR A PARTICULAR PURPOSE.  THE ENTIRE RISK AS TO THE QUALITY AND 
# PERFORMANCE OF THE PROGRAM IS WITH YOU.  SHOULD THE PROGRAM PROVE DEFECTIVE, 
# YOU ASSUME THE COST OF ALL NECESSARY SERVICING, REPAIR OR CORRECTION.
# 
#   12. IN NO EVENT UNLESS REQUIRED BY APPLICABLE LAW OR AGREED TO IN WRITING 
# WILL ANY COPYRIGHT HOLDER, OR ANY OTHER PARTY WHO MAY MODIFY AND/OR 
# REDISTRIBUTE THE PROGRAM AS PERMITTED ABOVE, BE LIABLE TO YOU FOR DAMAGES, 
# INCLUDING ANY GENERAL, SPECIAL, INCIDENTAL OR CONSEQUENTIAL DAMAGES ARISING 
# OUT OF THE USE OR INABILITY TO USE THE PROGRAM (INCLUDING BUT NOT LIMITED TO 
# LOSS OF DATA OR DATA BEING RENDERED INACCURATE OR LOSSES SUSTAINED BY YOU OR 
# THIRD PARTIES OR A FAILURE OF THE PROGRAM TO OPERATE WITH ANY OTHER PROGRAMS), 
# EVEN IF SUCH HOLDER OR OTHER PARTY HAS BEEN ADVISED OF THE POSSIBILITY OF SUCH 
# DAMAGES.
# 
# 		     END OF TERMS AND CONDITIONS
# */

# (C) Copyright Michigan State University 1937, All rights reserved 
#
#  Package to provide a gui for the readout software.  Includes
#  a GUI to select the readout program. The assumptions are:
#  
#  o Readout program will/may run on a remote system (over an rsh pipe).
#  o If readout program runs on a remote system, it completely shares
#    the user's directory structure.
#
#   Change log:
#
# $Log$
# Revision 3.5  2004/10/29 20:32:57  ron-fox
# Merge the 7.4 development into the main line
#

# Revision 3.2.4.3  2004/07/22 14:05:10  ron-fox
# Add a validator to ensure that run numbers are actually numbers
# otherwise the status line can blow up trying to format a textual
# run number into the filenames it looks for.
#
# Revision 3.2.4.2  2004/04/06 13:18:13  ron-fox
# fix for issue 108 GUI allows overwrite of event file without prompt
#
# Revision 3.2.4.1  2004/03/31 18:30:10  ron-fox
# Address issue 108 - Event files can be ovewritten without any prompts.
#                     added issue - contrast of disabled entries is bad.
#
# Revision 3.2  2003/08/14 19:01:55  ron-fox
# Multiple changes:
# 1. Fix support of segmented event files.
# 2. Fix small nigglling errors in the tape number/file numbering.
# 3. Ensure only one instance of Stager is running per user per
#    instance of a shared filesystem (.lock files).
# 4. Do tar I/O via file events so the user interface is fully live
#    (needed for large event files).
#
# Revision 3.1  2003/03/22 04:03:55  ron-fox
# Added SBS/Bit3 device driver.
#
# Revision 2.2  2003/03/12 03:46:42  ron-fox
#    Added an event binding to the <Destroy> Event  the binding calls a new procedure:  EmergencyExit that in turn forces the Readout program to exit.  The exit is done via the normal ReadoutControl::ExitReadoutProgram.  That ends the run if possible (which will allow the event file to properly close even if the directory manipulations don't happen). And if possible Readout is exited cleanly.  Regardless, the socket is actively shutdown, so Readout won't hang around until timeout, but will exit immediately.
#
#
package provide ReadoutGui 1.0
package require Diagnostics
package require ExpFileSystem

namespace eval ReadoutGui {
    variable ElapsedTime "0-00:00:00"
    variable ElapsedTimer 0
    variable isTimed     0
    variable RequestedDuration 0-00:00:00
    variable ActiveGhostedWidgets
    variable ActiveGhostedMenus
    variable EventStatusLineWidget
    variable EventFileStatusLine
    variable OutputWidget
    variable OutputSavedLines 500
    variable FileMenu
    variable BeginButton
    variable PauseButton
    variable RecordToggle
    
    # Run number entry edit validation proc.
    # This procedure ensures that any edit string
    # inserted will be entirely numeric.
    #
    #   final - The resulting contents of the entry.

    #
    proc ValidateRun {final} {
	if {[regexp ^\[0-9\]+\$ $final]} {
	    return 1
	} else {
	    return 0
	}
    }

    #  Improve the contrast of a disabled entry widget by
    #  setting its disabled foreground color to the normal foreground
    #  color.  Entry widgets have a disbled background color which provides
    #  sufficient contrast.
    proc ImproveEntryContrast {widget} {
	global tk_version
	scan $tk_version "%d.%d" major minor
	# Ensure we have the functionality we need for this...

	if { ($major > 8) || (($major == 8) && ($minor >= 4)) } {
	    $widget config -disabledforeground [$widget cget -foreground]
	}
    }

    #  When called, this proc:
    #  - turns off event recording.
    #  - ghosts the event recording toggle button.
    #  - Removes the event recording toggle from the 
    #    ActiveGhostedWidgets list.
    # Note that it is the caller's responsibility to invoke this
    # only if the run is halted.
    #
    proc DisableEventRecording {} {
	variable RecordToggle
	variable ActiveGhostedWidgets

	ReadoutControl::DisableTape;	# Turn off event recording.
	$RecordToggle config -state disabled
	set listposition \
	    [lsearch -exact $ActiveGhostedWidgets $RecordToggle]
	if {$listposition != -1} {
	    set ActiveGhostedWidgets [lreplace $ActiveGhostedWidgets \
					  $listposition $listposition]
	}
    }
    #  When called this proc:
    #  - Unghosts the event recording toggle button.
    #  - If necessary adds the recording toggle button to the
    #    ActiveGhostedWidgets list.
    # NOTE: It is the caller's responsibility to invoke this only 
    #     when the run is halted.
    #
    proc EnableEventRecording {} {
	variable RecordToggle
	variable ActiveGhostedWidgets

	$RecordToggle config -state normal
	set position \
	    [lsearch -exact $ActiveGhostedWidgets $RecordToggle]
	if {$position == -1} {
	    lappend ActiveGhostedWidgets $RecordToggle
	}

    }
    #  Display a dialog that asks what to do if we would
    #  otherwise overwrite event data?
    # Returns:
    #     0    - Don't start the run.
    #     1    - Start the run but don't record data.
    #     2    - Start the run and overwrite the data.
    #
    proc OverwriteCheck {} {
	set run [ReadoutControl::GetRun]
	if {[Experiment::RunFileExists $run]} {
	    set testrun $run
	    incr testrun
	    while {[Experiment::RunFileExists $testrun]} {
		incr testrun
	    }
	    set message \
		"WARNING: Event data for run $run already exists! "
	    append message \
		" If you continue to start this run with event "
	    append message "recording enabled, you will overwrite "
	    append message "this file. "
	    append message "Note that the next run number without "
	    append message "recorded event data is $testrun. "
	    append message  "Please select an "
	    append message "appropriate action:"
	    return [tk_dialog .ovewrite "!!Overwrite!!" $message \
			warning 0 \
			"Don't start" \
			"Start without recording" \
			"Start and overwrite"]

	} else {
	    return 2;			# No problems starting run.
	}
	    
    }
    proc NoOp {} {}

    # Update links: called to update the
    # event file links in the current directory.
    # the parameter is the set of event file segments.
    proc UpdateLinks {segments} {
	set ExpCurrent [ExpFileSystem::WhereisCurrentData]
	foreach segment $segments {
	    set basename [file tail $segment]
	    set linktarget $ExpCurrent/$basename
	    if {![file exists $linktarget] } {
		catch "exec ln -s $segment $linktarget"
	    }
	}
    }

    #
    # Save Readout Settings:

    proc SaveSettings {} {
	set file [ExpFileSystem::GetRoot]/.readoutconfig
	set fd [open $file w]
	puts $fd "set title \"[ReadoutControl::GetTitle]\""
	puts $fd "set run   [ReadoutControl::GetRun]"
	puts $fd "set scalers $ReadoutControl::Scalers"
	puts $fd "set period  $ReadoutControl::ScalerPeriod"

	close $fd
    }
    #
    #   Proc to source a file into the readout program.
    #   The file contains arbitrary readout program commands.
    #   this is especially intended for production readout.
    #
    proc SourceFile {} {
	#
	#  Accept the file:
	#
	set script [tk_getOpenFile -defaultextension .tcl \
		                  -title "Source file: " \
				  -filetypes {
			      {{Tcl Scripts} {.tcl}}
			      {{Tk Scripts}  {.tk}}
			      {{All Files}   {*}}
			  }]
	if {$script != ""} {
	    set sfd [open $script r]
	    set contents [read $sfd]
	    ReadoutControl::SendCommand $contents
	}
	
    }
    #
    #  Utility function to see if the requested duration is valid.
    #
    proc ValidTime {} {
	variable isTimed
	variable RequestedDuration

	if {!$isTimed} {return 1}
	set nconvert [scan $RequestedDuration "%d-%d:%d:%d" d h m s]
	if {$nconvert != 4} { 
	    return 0 
	} else {
	    return 1
	}
    }
    #
    #  utility functions to handle ghosting/unghosting lists of stuff.
    #
    proc setwidgetstates {list state} {
	foreach widget $list {
	    $widget config -state $state
	}
    }
    proc setmenustates {menus state} {
	foreach entry $menus {
	    set menu [lindex $entry 0]
	    set item [lindex $entry 1]
	    $menu entryconfigure  $item -state $state 
	}
    }
    #
    #  Elapsed run timer:
    #
    proc TimeToEndRun {} {
	variable RequestedDuration
	variable ElapsedTime
	
	scan $RequestedDuration "%d-%d:%d:%d" Rd Rh Rm Rs
	scan $ElapsedTime       "%d-%d:%d:%d" d h m s

	if {$Rd > $d} {return 0}
	if {$Rh > $h} {return 0}
	if {$Rm > $m} {return 0}
	if {$Rs > $s} {return 0}
	return 1
    }
    proc ClearElapsedTime {} {
	variable ElapsedTime
	set ElapsedTime "0-00:00:00"
    }
    proc StartElapsedTimer {begin pause} {
	variable ElapsedTimer 
	set ElapsedTimer [after 1000 ReadoutGui::SecondElapsed $begin $pause]
    }
    proc StopElapsedTimer {} {
	variable ElapsedTimer
	if {$ElapsedTimer != 0} {
	    after cancel $ElapsedTimer
	    set ElapsedTimer 0
	}
    }
    proc SecondElapsed {begin pause} {
	variable ElapsedTime
	variable ElapsedTimer
	variable EventFileStatusLine

	scan $ElapsedTime "%d-%d:%d:%d" Days Hrs Min Sec
	if {[incr Sec] >= 60} {
	    set Sec 0
	    if {[incr Min] >= 60} {
		set Min 0
		if {[incr Hrs] >= 24} {
		    set Hrs 0
		    incr Days
		}
	    }
	}
	#
	#  Set the run elapsed time.
	#
	set ElapsedTime [format "%d-%02d:%02d:%02d" $Days $Hrs $Min $Sec]
	#
	#  Update the status line for the event file:
	#  We need to sum all the sizes for all the segments.
	#  summing is done in Kbyte units in order to prevent overflows.
	#  We also invoke UpdateLinks to ensure that all event file links
	#  are valid.
	#
	set EventFile $Experiment::EventFilePath
	set EventDir  [file dirname $EventFile]
	set filepat $EventDir
	append filepat "/" \
		[::ExpFileSystem::GenRunFileBase $ReadoutControl::RunNumber] \
		 *
	set segments [glob -nocomplain $filepat]
	set nsegments [llength $segments]
	set size     0.0
	foreach segment $segments {
	    set status [catch {set segmentsize [file size $segment]}]
	    if {$status == 0} {
		set size [expr $size + $segmentsize/1024.0]
	    }
	}
	
	#  Now compute size in megabytes:

	set size [expr $size/1024.0]
	if {$nsegments > 0} {
	    UpdateLinks $segments
	    set EventFileStatusLine \
		    [format "Run %d recorded in %d segments totalling %9.2f Mbytes" \
		             $ReadoutControl::RunNumber $nsegments $size]
	} else {
	    set EventFileStatusLine \
		    [format "No run file segments for run %d yet" \
		            $ReadoutControl::RunNumber]
	}
	#
	# Repropagate self:
	#
	set ElapsedTimer [after 1000 ReadoutGui::SecondElapsed $begin $pause]
	
	#
	#  If the run is timed, see if it's time to end it:
	#  Need to do this now so that we can cancel self.
	#
	variable isTimed

	if {$isTimed} {
	    if {[TimeToEndRun]} {End $begin $pause}
	}
	
    }
    #  Respond to input from the Readout program:  Accept and append to the
    #  OutputWidget text widget.
    #
    proc ReadoutInput {fd} {
	variable OutputWidget
	if {![eof $fd]} {
	    $OutputWidget config -state normal
	    $OutputWidget insert end [read $fd 1000] ;# Assume fd is nonblock.
	    $OutputWidget see end
	    update idle
	    $OutputWidget config -state disabled
	}
    }
    proc OutputText {text} {
	variable OutputWidget
	variable OutputSavedLines
	set ticks [clock seconds]
	set timestamp [clock format $ticks]
	append timestamp " " $text "\n"
	$OutputWidget config -state normal
	$OutputWidget insert end $timestamp
	$OutputWidget see end
	update idle
	$OutputWidget config -state disabled

	# trim the text widget to save only the last 500 lines of text:
	
	if {[$OutputWidget compare $OutputSavedLines.0 < end]} {
	    $OutputWidget delete 0.0 {end - $OutputSavedLines lines}
	}
    }
    #
    # Called with a command sent to Readout. This is put in the
    # text window with a trailing newline.
    proc RdoCommand {line} {
	append line "\n"
	OutputText $line
    }
    #
    # Respond to Ok Button on Selector:
    #
    proc Ok {procedure container hostframe progframe} {
	set host [$hostframe.entry get]
	set program [$progframe.entry get]
	$procedure $container $host $program   ;#  Execute the callback.
    }
    #
    # Initiate browse for the filename.
    #
    proc Browse {frame} {
	$frame.entry delete  0 end 
	$frame.entry  insert  0 \
		[tk_getOpenFile -initialdir ~/experiment/current]
    }
    #
    # Utility to determine if the readout controller already has the host
    #
    proc HaveReadout {} {
	if { ($ReadoutControl::ReadoutHost == "") || \
		($ReadoutControl::ReadoutProgram == "")} {
	    return 0
	} else { return 1}
    }
    #
    # Dialog to require both host and program for readout.
    #
    proc NeedHostandProgram {} {
	tk_dialog .hosterror "Readout Program" \
		"Both the hostname and the readout program are required" \
		error 0 "Dismiss"
    }
    #
    # GUI entry to select readout program.
    #   container - container widget (frame or toplevel) in which the
    #               GUI is built.
    #   callback  - Proc to calback when info is in.
    #               parameters to the callback are container host path.
    #
    proc GetReadoutSpec {container callback} {

	set hostf [frame $container.hostframe -relief groove -bd 3]
	label $hostf.label -text "Readout Host: "
	entry $hostf.entry

	set progf [frame $container.programframe -relief groove -bd 3]
	label $progf.label -text "Readout File: "
	entry $progf.entry -width 40
	button $progf.browse -text "Browse..." \
		-command "ReadoutGui::Browse $progf"

	set actionf [frame $container.actions -relief groove -bd 3]
	button $actionf.ok -text "Ok" \
		-command "ReadoutGui::Ok $callback $container $hostf $progf"

	pack $hostf -fill x
	pack $hostf.label $hostf.entry -side left
	pack $progf -fill x
	pack $progf.label $progf.entry $progf.browse -side left
	pack $actionf -fill x
	pack $actionf.ok -side left -anchor c
	
    }
    #
    #  Called on exit:
    #  Provides a confirmation dialog...
    #
    proc Exit {} {
	set exit [tk_dialog .exitconfirmation \
		     "Exit ?" \
		     "Are you sure you want to exit?" \
		     questhead \
		     0 Yes No]
	if {$exit == 0} {
	    ReadoutControl::SetOnExit ReadoutGui::NoOp
	    ReadoutControl::ExitReadoutProgram
	    exit
	}
    }
    #
    # Called to do an emergency exit if the window is being destroyed.
    # We don't get to confirm this alas.
    #
    proc EmergencyExit {} {
	ReadoutControl::SetOnExit ReadoutGui::NoOp
	ReadoutControl::ExitReadoutProgram

	# Exit will happen automatically.
    }
    #
    # Processes new dialog box entry.
    # Brings up a dialog which selects a new front end host and program.
    # 
    proc NewDialog {} {
	toplevel .newdialog -class Dialog
	GetReadoutSpec .newdialog ReadoutGui::NewGotten
    }
    proc NewGotten {container host path} {
	ReadoutControl::SetReadoutProgram $host $path
	destroy $container
    }
    #
    #  This is called when the readout program exits.  It determines
    #  what if any cleanup is needed as a result of readout dying potentially
    #  in the middle of a run.
    proc CleanupRun {State} {
	# If is not ended, need to do emergency end run.
	#
	if {$State != "NotRunning"} {
#	    puts "Forcing an emergency end run...."
	    Experiment::EmergencyEnd
	    if {[ReadoutControl::isTapeOn]} {
		set nrun [ReadoutControl::GetRun]
		incr nrun
		ReadoutControl::SetRun $nrun
	    }
	}
    }
    #
    #   The readout program exited.  We need to ghost all the stuff
    #  which enabled on active.
    #  We need to change the restart menu entry to a Start entry.
    #
    proc ReadoutExited {RunStateonExit} {
	variable ActiveGhostedWidgets
	variable ActiveGhostedMenus
	variable FileMenu
	variable BeginButton
	variable PauseButton


	Diagnostics::Warning "Readout program exited."

	CleanupRun $RunStateonExit

	setwidgetstates $ActiveGhostedWidgets normal
	setmenustates $ActiveGhostedMenus     normal
	RunEnding     $BeginButton $PauseButton
	$BeginButton config -state disabled
	$PauseButton config -state disabled

	$FileMenu entryconfigure "Restart..." -label Start \
	     -command "ReadoutGui::Start $FileMenu $BeginButton  $PauseButton"

    }
    #
    #  Starts/restarts the front end program.
    #
    proc Start {menu beginbutton pausebutton} {
	if {![HaveReadout]} {
	    NeedHostandProgram
	    return
	}
	$menu entryconfigure Start -label "Restart..." \
		-command "ReadoutGui::Restart $menu $beginbutton $pausebutton"
	ReadoutControl::StartReadoutProgram
	RunEnding $beginbutton $pausebutton

    }
    proc Restart {menu beginbutton pausebutton} {
	if {![HaveReadout]} {
	    NeedHostandProgram
	    return 
	}
	set restart \
		[tk_dialog .restart "Restart" \
	"If you restart any currently active run will be ended.  Do you want to Restart?" \
	           questhead 1 Restart Cancel]
	if {$restart == 0} {
	    ReadoutControl::SetOnExit ReadoutGui::NoOp
	    ReadoutControl::ExitReadoutProgram
	    ReadoutControl::StartReadoutProgram
	    ReadoutControl::SetOnExit ReadoutGui::ReadoutExited
	    RunEnding $beginbutton $pausebutton
	}
	

    }
    #
    #  Dialog for accepting scaler parameters.
    #
    proc Scalers {} {
	toplevel .scalers -class Dialog
	wm       title .scalers "Scaler Parameters"
	set controls [frame    .scalers.controls -relief groove -borderwidth 2]
	set action   [frame    .scalers.action   -relief groove -borderwidth 2]
	label $controls.lblscalers -text "Scalers: "
	entry $controls.entscalers
	$controls.entscalers insert 0 $ReadoutControl::Scalers

	label $controls.lblperiod  -text "Period : "
	entry $controls.entperiod
	$controls.entperiod insert 0 $ReadoutControl::ScalerPeriod

	button $action.ok -text "Ok" \
	  -command "ReadoutGui::NewScalerParams $controls.entscalers $controls.entperiod"
	button $action.cancel -text "Cancel" -command "destroy .scalers"

	pack $controls
	grid configure $controls.lblscalers $controls.entscalers -row 0
	grid configure $controls.lblperiod  $controls.entperiod  -row 1

	pack $action
	pack $action.ok $action.cancel -side left
	
    }
    proc NewScalerParams {countwidget timewidget} {
	set Counttxt [$countwidget get]
	set Timetxt  [$timewidget get]

	if {[scan $Counttxt "%d" Countval] == 0} {
	    Diagnostics::Error \
		    "The number of scalers must be a  positive integer"
	    return
	}
	if {$Countval < 0} {
	    Diagnostics::Error \
		    "The number of scalers must be a  positive integer"
	    return
	}

	if {[scan $Timetxt "%d" Timeval] == 0} {
	    Diagnostics::Error \
		    "time interval between readouts must be an integer > 0"
	    return
	}
	if {$Timeval <= 0} {
	    Diagnostics::Error \
		    "The time interval between readouts must be an integer > 0"
	    return
	}
	destroy .scalers
	set ReadoutControl::Scalers $Countval
	set ReadoutControl::ScalerPeriod $Timeval
    }
    #
    #  Begin: Starts the run. RunStarting: Gui part of this process.
    #
    proc RunStarting {beginbutton pausebutton} {
	variable ActiveGhostedWidgets
	variable ActiveGhostedMenus
	variable EventStatusLineWidget
	variable EventFileStatusLine
	variable OutputWidget

	setwidgetstates $ActiveGhostedWidgets disabled
	setmenustates   $ActiveGhostedMenus   disabled
	ClearElapsedTime
	StartElapsedTimer $beginbutton $pausebutton

	$beginbutton config -text "End" \
		-command "ReadoutGui::End $beginbutton $pausebutton"
	$pausebutton config -state normal \
		-command "ReadoutGui::Pause $beginbutton $pausebutton"

	OutputText "Run Starting"
	SaveSettings
	if {[ReadoutControl::isTapeOn]} {
	    $EventStatusLineWidget config -bg green
	    $OutputWidget config -bg green
	} else {
	    set EventFileStatusLine ""
	}
    }
    proc Begin {beginbutton pausebutton} {
	variable RequestedDuration
	#  If necessary, check to see what to do
	#  if the user is ovewriting:
	#
	if {[ReadoutControl::isTapeOn]} {
	    after 1000
	    set action [OverwriteCheck]
	    if {$action == 0} {;	# Run aborted.
		return
	    }
	    if {$action == 1} {;	# Turn of tape.. but run.
		ReadoutControl::DisableTape
	    }
	    #  2 overwrite if needed.
	}
	#
	#  If it's a timed run, validate the time:
	#
	if {![ValidTime]} {
	    Diagnostics::Error "The run duration $RequestedDuration is badly formatted\n"
	    
	} else {
	    $beginbutton config -state disabled
	    RunStarting $beginbutton $pausebutton
	    ReadoutControl::Begin
	    ReadoutControl::ShowAll
	    after 5000 "$beginbutton config -state normal"
	}
    }
    #
    #  Pause: Pauses the run.  RunPausing: Gui part of this process.
    #
    proc RunPausing {beginbutton pausebutton} {
	$pausebutton config -text Resume \
		-command "ReadoutGui::Resume $beginbutton $pausebutton"
	StopElapsedTimer
	OutputText "Run Paused"
    }
    proc Pause {beginbutton pausebutton} {
	RunPausing $beginbutton $pausebutton
	ReadoutControl::Pause
	ReadoutControl::ShowAll
    }
    # Resume: Resumes a paused run.  RunResuming: Gui part of this process.
    #
    proc RunResuming {beginbutton pausebutton} {
	StartElapsedTimer $beginbutton $pausebutton
	$pausebutton config -text Pause \
		-command "ReadoutGui::Pause $beginbutton $pausebutton"
	OutputText "RunResumed"
    }
    proc Resume {beginbutton pausebutton} {
	RunResuming $beginbutton $pausebutton
	ReadoutControl::Resume
	ReadoutControl::ShowAll
    }
    #  End: ends the run RunEnding: Gui part of this process.
    #
    proc RunEnding {beginbutton pausebutton} {
	variable ActiveGhostedWidgets
	variable ActiveGhostedMenus
	variable EventStatusLineWidget
	variable OutputWidget

	StopElapsedTimer
	setwidgetstates $ActiveGhostedWidgets normal
	setmenustates   $ActiveGhostedMenus   normal

	$beginbutton config -text "Begin" \
		-command "ReadoutGui::Begin $beginbutton $pausebutton" \
		-state normal
	$pausebutton config -text "Pause" -state disabled -command ""
	$EventStatusLineWidget config -bg [. cget -bg]
	$OutputWidget config -bg [. cget -bg]
    }
    proc End {beginbutton pausebutton} {
	RunEnding $beginbutton $pausebutton
	$beginbutton config -state disabled
	ReadoutControl::End
	if {[ReadoutControl::isTapeOn]} {
	    set nrun [ReadoutControl::GetRun]
	    ReadoutControl::SetRun [incr nrun]
	}
	OutputText "Run Ended"
	ReadoutControl::ShowAll
	$beginbutton config -state normal

	SaveSettings
	
    }
    #
    #  Top level GUI for the run control.
    #
    proc ReadoutController {topname} {
	variable ActiveGhostedWidgets
	variable ActiveGhostedMenus
	variable OutputWidget
	variable FileMenu
	variable BeginButton
	variable PauseButton
	variable EventStatusLineWidget
	variable RecordToggle

	package require ReadoutControl
	package require Diagnostics

	ReadoutControl::GetRun     ;# Force package load.

	#   Build the window and menu structure.
	#

	# there are two possibilities.  The topname is . in which case
	# the interface is built into the base tk window, or it isn't in 
	# which case we'll pop up our own toplevel and build the gui into that.
	# regardless, a destroy handler is established to ensure that 
	# any readout program is killed off.
	#
	if {$topname != "."} {
	    set toplevelname $topname
	    toplevel $toplevelname
	    bind $toplevelname <Destroy> EmergencyExit
	} else {
	    set toplevelname ""
	    bind . <Destroy> EmergencyExit
	}
	wm title $topname "Run Control"

	#
	#  Build the program frame:
	#
	set pgmframe \
		[frame $toplevelname.program -relief groove -borderwidth 2]
	label $pgmframe.host -text "Host: "
	label $pgmframe.hostname -textvariable ReadoutControl::ReadoutHost
	label $pgmframe.program -text "Program: "
	label $pgmframe.programpath \
		-textvariable ReadoutControl::ReadoutProgram
	pack $pgmframe -side top
	pack $pgmframe.host $pgmframe.hostname $pgmframe.program \
		$pgmframe.programpath -side left
	#
	#  Much of the meat of the interface lives in the control status frame
	#  it contains several internal invisible frames used to place
	#  the controls.
	#
	set ctlframe  \
	     [frame $toplevelname.controlframe -relief groove -borderwidth 2]
	set titleframe [frame $ctlframe.title]
	label $titleframe.title -text "Title: "
	entry $titleframe.titlestring \
		-textvariable ReadoutControl::RunTitle \
		-width 50
	set rnotape [frame $ctlframe.rnotape]
	set RecordToggle [checkbutton $rnotape.taping \
			      -text "Record Events" \
			      -variable ReadoutControl::Taping]
	label $rnotape.run -text "Run number"
	entry $rnotape.runnumber \
		-textvariable ReadoutControl::RunNumber \
	    -validate key -validatecommand "ReadoutGui::ValidateRun %P" \
	    -invalidcommand bell

	set buttonframe \
		[frame $ctlframe.buttonframe  -relief groove -borderwidth 2]
	button $buttonframe.startstop -text Begin \
               -command \
	       "ReadoutGui::Begin $buttonframe.startstop $buttonframe.pauseres"
	button $buttonframe.pauseres  -text Pause -state disabled
	set BeginButton $buttonframe.startstop
	set PauseButton $buttonframe.pauseres

	set timeframe [frame $ctlframe.timeframe -relief groove -borderwidth 2]
	checkbutton $timeframe.timed -text timed -variable ReadoutGui::isTimed
	label       $timeframe.time -text "Run Duration: "
	entry       $timeframe.reqtime \
		-textvariable ReadoutGui::RequestedDuration \
		-state disabled
	label       $timeframe.elapsed -text "Elapsed time: "
	label       $timeframe.elapsedval -textvariable ReadoutGui::ElapsedTime

	set rctlframe \
		[frame $toplevelname.rctlframe -relief groove -borderwidth 2]
	set OutputWidget [text $rctlframe.output -width 70 -state disabled \
		-yscrollcommand "$rctlframe.scrollbar set"]
	scrollbar $rctlframe.scrollbar -orient vertical \
		-command "$rctlframe.output yview"

	set EventStatusLineWidget [
	   label     $toplevelname.evstatusline \
		     -textvariable ReadoutGui::EventFileStatusLine
	]

	pack $ctlframe -side top -fill x
	pack $titleframe $rnotape $buttonframe -side top -fill x
	pack $titleframe.title $titleframe.titlestring -side left
	pack $rnotape.taping $rnotape.run $rnotape.runnumber -side left
	pack $buttonframe.startstop  -side left
	pack $buttonframe.pauseres   -side right
	pack $timeframe -side top
	pack $timeframe.timed $timeframe.time $timeframe.reqtime \
		$timeframe.elapsed $timeframe.elapsedval -side left
	pack $rctlframe -side top
	pack $rctlframe.output -side left -fill x
	pack $rctlframe.scrollbar -side right -fill y
	pack  $toplevelname.evstatusline  -side top -fill x
	#
	# build the menubar`:
	#
	set menubar [menu $toplevelname.menubar -tearoff 0]
	$topname config -menu $menubar
	set filemenu [menu $menubar.file -tearoff 0]
	set FileMenu $filemenu
	$menubar add cascade -label File -menu $filemenu
	set scalermenu [menu $menubar.scaler -tearoff 0]
	$menubar add cascade -label Scalers -menu $scalermenu
	$filemenu add command -label "New..." -command ReadoutGui::NewDialog
	$filemenu add command -label Start    \
		-command \
 	                    "ReadoutGui::Start $filemenu $buttonframe.startstop $buttonframe.pauseres"
	$filemenu add separator
	$filemenu add command -label Source -command \
		ReadoutGui::SourceFile

	$filemenu add separator
	$filemenu add command -label Exit -command ReadoutGui::Exit

	$scalermenu add command -label "Parameters..." \
		-command ReadoutGui::Scalers

	#
	#  Items to ghost when a run is active:
	#
	set ActiveGhostedWidgets " $titleframe.titlestring
	                          $rnotape.taping
	                          $rnotape.runnumber
	                          $timeframe.timed
	                          $timeframe.reqtime
                                "
	lappend ActiveGhostedMenus "$filemenu New..." \
		                   "$scalermenu Parameters..."

	
	# The contrast of ghosted entry widgets (titlestring, runnumber
	# and reqtime is not good.  We improve this by setting the
	# disabled foreground color of these widges to be the same as
	# their normal foreground.

	ImproveEntryContrast $titleframe.titlestring
	ImproveEntryContrast $rnotape.runnumber
	ImproveEntryContrast $timeframe.reqtime


	if {[HaveReadout]} {
	    Start $filemenu $buttonframe.startstop $buttonframe.pauseres
	}
	# Recover file settings for scaler, title, run
	
	set title "Set a new title"
	set run     [Experiment::BiggestRun]
	set scalers 0
	set period 10
	set file [ExpFileSystem::GetRoot]/.readoutconfig
	if {[file exists $file]} {source $file}
	ReadoutControl::SetTitle $title
	ReadoutControl::SetRun   $run
	set ReadoutControl::Scalers $scalers
	set ReadoutControl::ScalerPeriod $period
	

	# Establish callback handlers for input from the Readout Program:
	#
	ReadoutControl::SetOnInput ReadoutGui::ReadoutInput
	#
	#  Let the experiment manager tell us what to use as the first run.
	#
	ReadoutControl::SetOnExit "ReadoutGui::ReadoutExited"
	ReadoutControl::SetOnCommand "ReadoutGui::RdoCommand"




    }
    
    # Exported functions:

    namespace export GetReadoutSpec 
    namespace export ReadoutController
    namespace export  ReadoutInput
    namespace export DisableEventRecording
    namespace export EnableEventRecording
}


