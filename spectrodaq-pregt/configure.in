dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/spectrodaq.h)

AC_LANG_CPLUSPLUS

dnl Define a g++ version check macro
AC_DEFUN([AC_GXX_VERSION_CHECK],
[CXXVEROK=`echo $CXX_STR_VERSION | [sed 's/2\.9[5-6]\.[[:digit:]+]/yes/'] | [sed 's/3\.2\.[[:digit:]]/yes/'] | [sed 's/3\.3\.[[:digit:]]/yes/']`])

dnl Define a cpp version check macro
AC_DEFUN([AC_CPP_VERSION_CHECK],
[CPPVEROK=`echo $CPP_STR_VERSION | [sed 's/2\.9[5-6]\.[[:digit:]+]/yes/'] | [sed 's/3\.3\.[[:digit:]]/yes/']`])

dnl Define a g++ check macro for g++ 2.96.xx
AC_DEFUN([AC_GXX_VERSION_296],
[CXXVER296=`echo $CXX_STR_VERSION | [sed 's/2\.96\.[[:digit:]+]/yes/'] | [sed 's/2\.96/yes/']`])

dnl First let's do the package version
SPECTRODAQ_VERSION=`cat RELEASE`
AC_DEFINE_UNQUOTED(SPECTRODAQ_VERSION,"$SPECTRODAQ_VERSION")
AC_SUBST(SPECTRODAQ_VERSION)

dnl Now the client package version
SPECTRODAQ_CLIENT_VERSION=`cat RELEASECLIENT`
AC_DEFINE_UNQUOTED(SPECTRODAQ_CLIENT_VERSION,"$SPECTRODAQ_CLIENT_VERSION")
AC_SUBST(SPECTRODAQ_CLIENT_VERSION)

dnl and define the xmloop version
XMLPARSER_VERSION=`cat xmloop/RELEASE`
AC_DEFINE_UNQUOTED(XMLPARSER_VERSION,"$XMLPARSER_VERSION")
AC_SUBST(XMLPARSER_VERSION)

dnl and define the default config file
SPECTRODAQ_CONFIG=$prefix/etc/spectrodaq.conf
AC_DEFINE_UNQUOTED(SPECTRODAQ_CONFIG,"$SPECTRODAQ_CONFIG")
AC_SUBST(SPECTRODAQ_CONFIG)

TOPDIR=`pwd`
AC_SUBST(TOPDIR)

dnl need to do this before AC_PROG_CC and AC_PROG_CXX
gdb_debugging="yes"

AC_ARG_ENABLE(gdb-debugging,[  --enable-gdb-debugging (disabled by default) Use full gdb debugging when compiling],gdb_debugging="$enableval",gdb_debugging="no")

if test "$gdb_debugging" = "yes"; then
  CFLAGS="$CFLAGS -ggdb2 -g2"
  CXXFLAGS="$CXXFLAGS -ggdb2 -g2"
fi

dnl need to do this before AC_PROG_CC and AC_PROG_CXX
gprof_profiling="yes"

AC_ARG_ENABLE(gprof-profiling,[  --enable-gprof-profiling (disabled by default) Create gprof profiling data while compiling],gprof_profiling="$enableval",gprof_profiling="no")

if test "$gprof_profiling" = "yes"; then
  CFLAGS="$CFLAGS -pg"
  CXXFLAGS="$CXXFLAGS -pg"
fi

dnl Should we display compilation commands
build_verbose="no"
AT_BUILD="@"

AC_ARG_ENABLE(verbose-build,[  --enable-verbose-build (disabled by default) Allow compilation commands to be displayed while compiling],build_verbose="$enableval",build_verbose="no")

if test "$build_verbose" = "yes"; then
  AT_BUILD=""
fi

AC_DEFINE_UNQUOTED(AT_BUILD,"$AT_BUILD")
AC_SUBST(AT_BUILD)

dnl program checks
AC_PROG_CC(gcc) 
AC_PROG_CXX(g++)
AC_PROG_CXXCPP
AC_PROG_RANLIB
AC_CHECK_PROG(AR,ar,ar rs,:)
AC_CHECK_PROG(CP,cp,cp -f,:)
AC_CHECK_PROG(MAKEDEP,makedepend,makedepend -f .depend,:)
AC_CHECK_PROG(TOUCH,touch,touch,:)
AC_CHECK_PROG(INSTALL,install,install -m 0644,cp -f)
AC_CHECK_PROG(DIRINST,install,install -d -m 0755,cp -f)
AC_CHECK_PROG(BININST,install,install -m 0755,cp -f)
AC_CHECK_PROG(MKDIR,mkdir,mkdir -p,:)
AC_CHECK_PROG(DOXYGEN,doxygen,doxygen,:)

dnl If we didn't get the ar program then fail
if test "x$AR" = "x:"; then 
  AC_MSG_ERROR([
*** Can not locate program ar, don't know how to create libraries. 
Check 'config.log' for *** more details.])
fi

AC_SUBST(AR)
AC_SUBST(GXX)

dnl Allow the user to force compile with wrong compiler/versions
force_build="no"
AC_ARG_ENABLE(force-build,[  --enable-force-build (disabled by default) force build with wrong compiler/versions],force_build="$enableval",force_build="no")

GXX_TESTED_VERSIONS="2.95.xx 2.96.xx 3.3.3 3.3.xx"

dnl Not using g++ maybe trouble
if test "x$GXX" != "xyes" -a "x$force_build" != "xyes"; then
  AC_MSG_ERROR([
***********************************************************************
*** g++ (the GNU c++ compiler) is strongly suggested for compiling this 
*** program.  Check 'config.log' for *** more details.
*** Force using --enable-force-build.
***********************************************************************])
fi

dnl Maybe we want to compile with a different version of gcc
COMPILER_VERSION=

AC_MSG_CHECKING(for compiler Version flag) 
AC_ARG_WITH(compiler-version,[  --with-compiler-version(default \"\") version of the compiler to use for building],COMPILER_VERSION="-V $withval",COMPILER_VERSION="$COMPILER_VERSION")
AC_MSG_RESULT($COMPILER_VERSION)

AC_SUBST(COMPILER_VERSION)

dnl Check the g++ and cpp versions
CXX_STR_VERSION=`$CXX $COMPILER_VERSION -dumpversion`
AC_DEFINE_UNQUOTED(CXX_STR_VERSION,"$CXX_STR_VERSION")
AC_SUBST(CXX_STR_VERSION)

CPP_STR_VERSION=`$CXXCPP $COMPILER_VERSION -dumpversion`
AC_DEFINE_UNQUOTED(CPP_STR_VERSION,"$CPP_STR_VERSION")
AC_SUBST(CPP_STR_VERSION)


dnl Checks for compiler/system peculiarities
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AC_CHECK_SIZEOF(void *)
AC_C_BIGENDIAN
dnl AC_C_CONST
dnl AC_C_INLINE

dnl Checks for header files.
AC_CONFIG_HEADER(daqconfig.h)
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(sys/stat.h sys/types.h sys/mman.h)
AC_CHECK_HEADERS(fcntl.h limits.h unistd.h errno.h math.h ctype.h)
AC_CHECK_HEADERS(malloc.h netdb.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_PID_T

dnl Checks for library functions.
AC_FUNC_MMAP
AC_CHECK_FUNCS(mmap)
AC_CHECK_FUNCS(strerror)
AC_CHECK_FUNCS(snprintf)
AC_CHECK_FUNCS(set_new_handler)
AC_CHECK_FUNCS(set_unexpected)
AC_CHECK_FUNCS(set_terminate)
AC_CHECK_FUNCS(abort)
AC_CHECK_FUNCS(malloc) 
AC_CHECK_FUNCS(realloc) 
AC_CHECK_FUNCS(calloc) 
AC_CHECK_FUNCS(free) 
AC_EGREP_HEADER(malloc,stdlib.h,AC_DEFINE(HAVE_DECL_MALLOC),)
AC_EGREP_HEADER(realloc,stdlib.h,AC_DEFINE(HAVE_DECL_REALLOC),)
AC_EGREP_HEADER(calloc,stdlib.h,AC_DEFINE(HAVE_DECL_CALLOC),)
AC_EGREP_HEADER(free,stdlib.h,AC_DEFINE(HAVE_DECL_FREE),)
AC_EGREP_HEADER(h_errno,netdb.h,AC_DEFINE(HAVE_DECL_H_ERRNO),)

makedep_header=""
AC_CHECK_HEADER(g++-v3/iostream.h, makedep_header="$makedep_header -I/usr/include/g++-3")
AC_CHECK_HEADER(g++/iostream.h, makedep_header="-I/usr/include/g++")
AC_CHECK_HEADER(c++/iostream.h, makedep_header="$makedep_header -I/usr/include/c++")
AC_CHECK_HEADER(cxx/iostream.h, makedep_header="$makedep_header -I/usr/include/cxx")
AC_CHECK_HEADER(h++/iostream.h, makedep_header="$makedep_header -I/usr/include/h++")
AC_CHECK_HEADER(hxx/iostream.h, makedep_header="$makedep_header -I/usr/include/hxx")
AC_SUBST(makedep_header)

dnl Check for pstat
AC_CHECK_HEADER(sys/pstat.h, AC_DEFINE(HAVE_SYS_PSTAT_H), no_sys_pstat=yes)

dnl Check for shared memory
AC_CHECK_HEADER(sys/ipc.h, AC_DEFINE(HAVE_IPC_H), no_sys_ipc=yes)
AC_CHECK_HEADER(sys/shm.h, AC_DEFINE(HAVE_SHM_H), no_sys_shm=yes)
AC_CHECK_HEADER(sys/sem.h, AC_DEFINE(HAVE_SEM_H), no_sys_sem=yes)
AC_CHECK_HEADER(asm/shmparam.h, AC_DEFINE(HAVE_SHMPARAM_H), no_asm_shmparam=yes)

AC_MSG_CHECKING(for SysV shared memory) 

if test "$ac_cv_header_sys_shm_h" = "yes"; then
  AC_DEFINE(SYSV_IPC_SHM)
  AC_MSG_RESULT(yes)
else
  if test "$ac_cv_func_mmap" = "yes"; then
    AC_DEFINE(MMAP_SHM)
    AC_MSG_RESULT(no)
  else
    AC_MSG_ERROR([*** Neither SysV shared memory nor memory mapped files were found. Check 'config.log' for more details.])
  fi
fi

dnl Check for pthreads
AC_CHECK_HEADERS(pthread.h)
AC_CHECK_FUNCS(pthread_create)

no_pthreads="no"

if test "$ac_cv_header_pthread_h" = "yes"; then
  if test "$ac_cv_func_pthread_create" = "yes"; then
    no_pthreads="no"
    AC_DEFINE(USE_PTHREADS)
  else

    AC_CHECK_LIB(pthread, pthread_create,
      thread_libs="-lpthread",
      # Maybe libthread???
      AC_CHECK_LIB(thread, pthread_create,
          thread_libs="-lthread",
          no_pthreads=yes, $thread_libs),
      $thread_libs)

    if test "$no_pthreads" = "no"; then
      AC_DEFINE(USE_PTHREADS)
    else
      AC_MSG_ERROR([*** Found pthread.h but couldn't locate pthread_create() -- is a special library required?  Check 'config.log' for more details.]) 
    fi
  fi
else
    AC_MSG_ERROR([*** Couldn't locate pthread support. Check 'config.log' for more details.]) 
fi

AC_SUBST(thread_libs)

no_crypt="no"

# First check libc
AC_CHECK_LIB(c, crypt,
  # Maybe libcrypt???
  crypt_libs="",
  AC_CHECK_LIB(crypt, crypt,
    crypt_libs="-lcrypt",
    no_crypt=yes, $crypt_libs),
  $crypt_libs)

if test "$no_crypt" = "no"; then
  AC_DEFINE(USE_CRYPT)
else
  AC_MSG_ERROR([*** Couldn't find crypt() in libc or libcrypt?  Check 'config.log' for more details.]) 
fi

AC_SUBST(crypt_libs)

set_procname="yes"

AC_ARG_ENABLE(set-procname,[  --disable-set-procname (enabled by default) Set process status line name],set_procname="$enableval",set_procname="yes")

if test "$set_procname" = "no"; then
  AC_DEFINE(NO_SET_PROCNAME)
  AC_MSG_WARN(Setting of process status line name not enabled)
fi

daq_tracking="no"
daq_track_file="no"

AC_ARG_ENABLE(daq-tracking,[  --enable-daq-tracking (disabled by default) Track DAQ Object creation and deletion],daq_tracking="$enableval",daq_tracking="no")

if test "$daq_tracking" = "no"; then
  AC_DEFINE(NO_DAQ_TRACKING)
  AC_DEFINE(NO_DAQ_TRACK_FILE)
  AC_MSG_WARN(DAQ Object tracking not enabled)
fi

AC_ARG_ENABLE(daq-track-file,[  --enable-daq-track-file (disabled by default) Track file names and line numbers],daq_track_file="$enableval",daq_track_file="no")

if test "$daq_tracking" = "yes"; then
  if test "$daq_track_file" = "no"; then
    AC_DEFINE(NO_DAQ_TRACK_FILE)
    AC_MSG_WARN(Tracking file names and line numbers not enabled)
  fi
fi

delete_strstream="no"

AC_ARG_ENABLE(delete-strstream,[  --enable-delete-strstream (disabled by default) Delete dynamic strstream memory],delete_strstream="$enableval",delete_strstream="no")

if test "$delete_strstream" = "no"; then
  AC_DEFINE(NO_DELETE_STRSTREAM)
  AC_MSG_WARN(Deletion of dynamic strstream memory disabled.)
fi

daq_exectrack="yes"

AC_ARG_ENABLE(daq-exectrack,[  --disable-daq-exectrack (enabled by default) Track DAQ execution],daq_exectrack="$enableval",daq_exectrack="yes")

if test "$daq_exectrack" = "no"; then
  AC_DEFINE(NO_DAQ_EXECTRACKING)
  AC_MSG_WARN(DAQ Execution tracking not enabled)
fi

NSCL_CODE="$prefix"

AC_MSG_CHECKING(for NSCL code directory) 
AC_ARG_WITH(nscl-code,[  --with-nscl-code (default @prefix@) location of NSCL installed library],NSCL_CODE="$withval",NSCL_CODE="$prefix")
AC_MSG_RESULT($NSCL_CODE)

NSCL_CODE_INC="$NSCL_CODE/include"
NSCL_CODE_LIB="$NSCL_CODE/lib"

AC_SUBST(NSCL_CODE)
AC_SUBST(NSCL_CODE_INC)
AC_SUBST(NSCL_CODE_LIB)

dnl Allow the user to disable interface building
build_gui="no"

AC_ARG_ENABLE(gui-build,[  --enable-gui-build (disabled by default) build the GUI interface (not yet)],build_gui="$enableval",build_gui="no")

IFACE_SUBDIR=""

if test "$build_gui" = "yes"; then
  IFACE_SUBDIR="interface"
  AC_SUBST(IFACE_SUBDIR)

  GTK_DIR="/usr/local"

  AC_MSG_CHECKING(for GTK installation) 
  AC_ARG_WITH(gtk,[  --with-gtk (default /usr/local, gui-build must be enabled) location of installed GTK+ ],GTK_DIR="$withval",GTK_DIR="/usr/local")
  AC_MSG_RESULT($GTK_DIR)

  GTK_LIBS=`$GTK_DIR/bin/gtk-config --libs`
  GTK_CFLAGS=`$GTK_DIR/bin/gtk-config --cflags`

  AC_SUBST(GTK_LIBS)
  AC_SUBST(GTK_CFLAGS)
else
  IFACE_SUBDIR=""
  AC_SUBST(IFACE_SUBDIR)
  GTK_LIBS=""
  GTK_CFLAGS=""
  AC_SUBST(GTK_LIBS)
  AC_SUBST(GTK_CFLAGS)
fi

AC_OUTPUT(config.make xmloop/config.make scripts/spectrodaq-conf examples/Makefile doc/Doxyfile tests/test001 tests/test002 tests/test003 tests/test004 tests/test005 tests/test006 tests/test007 tests/test008 tests/test009 tests/test010 tests/test011)

dnl Check if c++ version warnings
CXXVER296="no"
AC_GXX_VERSION_296($CXX_STR_VERSION)
if test "x$CXXVER296" == "xyes"; then
  AC_MSG_WARN([
***********************************************************************
*** compilation of this program using g++ 2.96.xx is known to take  
*** a rather long time.  This problem appears to be a gcc 
*** problem/feature.  Compilation appears to produce a working
*** version of SpectroDAQ.  However, you may want to go to lunch
*** shortly after starting the compilation.
***********************************************************************])
fi

